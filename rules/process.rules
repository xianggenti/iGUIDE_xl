# -*- mode: Snakemake -*-
# Final Processing Rules

rule all_uniq_sites:
  input:
    expand(
      RUN_DIR + "/process_data/post_align/{sample}.{bin}.uniq.csv", 
      sample=SAMPLES, bin=BINS)
  output:
    temp(RUN_DIR + "/output/unique_sites." + RUN + ".csv")
  params:
    RUN_DIR + "/process_data/post_align"
  resources:
    mem_mb = lambda wildcards, attempt: attempt * config["defaultMB"]
  shell:
    """
    head -n 1 -q {params}/*.uniq.csv | uniq > {output}
    for UNIQ in $(ls {params}/*uniq.csv); do
        tail -n +2 $UNIQ >> {output}
    done
    """

rule combine_multihits:
  input:
    expand(
      RUN_DIR + "/process_data/post_align/{{sample}}.{bin}.multihits.rds",
      bin=BINS)
  output:
    hits=temp(RUN_DIR + "/process_data/multihits/{sample}.multihits.rds"),
    stat=temp(RUN_DIR + "/process_data/stats/{sample}.multihits.stat")
  params:
    tool=ROOT_DIR + "/tools/rscripts/combine_multihits.R",
    dir=RUN_DIR + "/process_data/post_align",
    pattern="{sample}.bin[0-9]+.multihits.rds"
  log:
    RUN_DIR + "/logs/{sample}.multihits.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["qualCtrlMB"]
  shell:
    """
    Rscript {params.tool} -d {params.dir} -p {params.pattern} \
      -o {output.hits} -s {output.stat} > {log} 2>&1
    """


def all_umitag_inputs(wildcards):
  if (config["UMItags"]):
    return expand(
      RUN_DIR + "/process_data/indices/{sample}.{bin}.umitags.fasta.gz", 
      sample=SAMPLES, bin=BINS)
  else:
    return []

def all_multi_inputs(wildcards):
  if (config["recoverMultihits"]):
    return expand(
      RUN_DIR + "/process_data/multihits/{sample}.multihits.rds", 
      sample=SAMPLES)
  else:
    return []

rule assimilate_sites:
  input:
    sites=RUN_DIR + "/output/unique_sites." + RUN + ".csv",
    umitag=all_umitag_inputs,
    multi=all_multi_inputs
  output:
    incorp=RUN_DIR + "/output/incorp_sites." + RUN + ".rds",
    stat=temp(RUN_DIR + "/process_data/stats/" + RUN + ".assim.stat")
  params:
    config=RUN_DIR + "/config.yml",
    tool=ROOT_DIR + "/tools/rscripts/assimilate_incorp_data.R",
    umitagDir=RUN_DIR + "/process_data/indices",
    multiDir=RUN_DIR + "/process_data/multihits",
    readNamePatternArg=config["readNamePattern"]
  log:
    RUN_DIR + "/logs/" + RUN + ".assim.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["assimilateMB"]
  run:
    call_str="Rscript {params.tool} {input.sites} -o {output.incorp} "
    if (config["UMItags"]):
        call_str=call_str + "-u {params.umitagDir} "
    if (config["recoverMultihits"]):
        call_str=call_str + "-m {params.multiDir} "
    call_str=call_str + "-c {params.config} --stat {output.stat} "
    call_str=call_str + "--readNamePattern {params.readNamePatternArg} > {log} 2>&1"
    shell(call_str)


rule iguide_evaluation:
  input: RUN_DIR + "/output/incorp_sites." + RUN + ".rds"
  output: 
    eval=temp(RUN_DIR + "/output/iguide.eval." + RUN + ".rds"),
    stat=temp(RUN_DIR + "/output/" + RUN + ".eval.stat")
  params: 
    tool = ROOT_DIR + "/tools/rscripts/evaluate_incorp_data.R",
    config = RUN_DIR + "/" + "config.yml"
  log: RUN_DIR + "/logs/" + RUN + ".eval.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["evaluateMB"]
  run:
    call_str="Rscript {params.tool} {params.config} -o {output.eval}"
    if (config["suppFile"]):
      call_str=call_str + " -s " + str(SUPPINFO_PATH)
    call_str=call_str + " --stat {output.stat} > {log} 2>&1"
    shell(call_str)


def report_supp(wildcards):
  supp_str = str()
  if (config["tables"]): 
      supp_str = supp_str + "-b "
  if (config["figures"]): 
      supp_str = supp_str + "-f "
  if (config["reportData"]):
      supp_str = supp_str + "-d "
  if (config["infoGraphic"]):
      supp_str = supp_str + "-g "
  return supp_str
        
rule run_report:
  input: RUN_DIR + "/output/iguide.eval." + RUN + ".rds"
  output: RUN_DIR + "/reports/report." + RUN + ".html"
  params: 
    tool = ROOT_DIR + "/tools/rscripts/generate_iGUIDE_report.R",
    config = RUN_DIR + "/" + "config.yml",
    supp = report_supp
  log: RUN_DIR + "/logs/" + RUN + ".report.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["reportMB"]
  shell:
    """
    Rscript {params.tool} {input} -o {output} {params.supp} > {log} 2>&1
    """


rule run_summary:
  input: RUN_DIR + "/output/iguide.eval." + RUN + ".rds"
  output: RUN_DIR + "/reports/summary." + RUN + ".txt"
  params: 
    tool = ROOT_DIR + "/tools/rscripts/generate_iGUIDE_summary.R",
    config = RUN_DIR + "/" + "config.yml"
  log: RUN_DIR + "/logs/" + RUN + ".summary.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["reportMB"]
  shell:
    """
    Rscript {params.tool} {input} -o {output} > {log} 2>&1
    """
